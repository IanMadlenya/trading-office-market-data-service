buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE"
        classpath "io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE"
    }
}

plugins {
    id "org.sonarqube" version "1.2"
}

apply from: "$rootDir/gradle/versioning.gradle"
apply from: "$rootDir/libraries.gradle"

apply plugin: "io.spring.dependency-management"
dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-starter-parent:Brixton.M5'
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply from: "$rootDir/gradle/jacoco.gradle"

sonarqube {
    properties {
        property "sonar.host.url", "https://sonar-nprogramming.rhcloud.com"
        property "sonar.projectKey", "trading-office-market-data-service"
        property "sonar.projectName", "Trading Office - Market Data Service"
    }
}


sourceCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://repo.spring.io/milestone'
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

dependencies {
    compile libs.spring.boot_web,
            libs.spring.boot_actuator,
            libs.spring.eureka_client,
            libs.okhttp,
            libs.finance_yahoo,
            libs.springfox.swagger_ui,
            libs.springfox.swagger2

    testCompile libs.test.junit,
            libs.test.mockito,
            libs.test.assertj
}

jar {
    baseName = 'trading-office-market-data-service'
    version = '1.0.0'
}

bootRun {
  systemProperties = System.properties
}

defaultTasks 'clean', 'bootRepackage'

task stage {
    dependsOn 'bootRepackage'
}
